# @<@PROJECT_NAME@>@ CMake configuration file:
# This file is meant to be placed in a cmake subfolder of @<@PROJECT_NAME@>@-devel-@<@PROJECT_VERSION@>@-VC.zip

include(FeatureSummary)
set_package_properties(SDL3_net PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_net/"
    DESCRIPTION "SDL_net is a simple, cross-platform wrapper over sockets"
)

cmake_minimum_required(VERSION 3.0...3.28)

# Copied from `configure_package_config_file`
macro(check_required_components _NAME)
    foreach(comp ${${_NAME}_FIND_COMPONENTS})
        if(NOT ${_NAME}_${comp}_FOUND)
            if(${_NAME}_FIND_REQUIRED_${comp})
                set(${_NAME}_FOUND FALSE)
            endif()
        endif()
    endforeach()
endmacro()

set(SDL3_net_FOUND                TRUE)


if(SDL_CPU_X86)
    set(_sdl3_net_arch_subdir "x86")
elseif(SDL_CPU_X64 OR SDL_CPU_ARM64EC)
    set(_sdl3_net_arch_subdir "x64")
elseif(SDL_CPU_ARM64)
    set(_sdl3_net_arch_subdir "arm64")
else()
    set(SDL3_net_FOUND FALSE)
    return()
endif()

set(_sdl3_net_incdir      "${CMAKE_CURRENT_LIST_DIR}/../include")
set(_sdl3_net_library     "${CMAKE_CURRENT_LIST_DIR}/../lib/${_sdl3_net_arch_subdir}/SDL3_net.lib")
set(_sdl3_net_dll         "${CMAKE_CURRENT_LIST_DIR}/../lib/${_sdl3_net_arch_subdir}/SDL3_net.dll")

# All targets are created, even when some might not be requested though COMPONENTS.
# This is done for compatibility with CMake generated SDL3_net-target.cmake files.

set(SDL3_net_SDL3_net-shared_FOUND TRUE)
if(NOT TARGET SDL3_net::SDL3_net-shared)
    add_library(SDL3_net::SDL3_net-shared SHARED IMPORTED)
    set_target_properties(SDL3_net::SDL3_net-shared
        PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${_sdl3_net_incdir}"
            IMPORTED_IMPLIB "${_sdl3_net_library}"
            IMPORTED_LOCATION "${_sdl3_net_dll}"
            COMPATIBLE_INTERFACE_BOOL "SDL3_SHARED"
            INTERFACE_SDL3_SHARED "ON"
    )
endif()

set(SDL3_net_SDL3_net-static_FOUND FALSE)

if(SDL3_net_SDL3_net-shared_FOUND OR SDL3_net_SDL3_net-static_FOUND)
    set(SDL3_net_SDL3_net_FOUND TRUE)
endif()

function(_sdl_create_target_alias_compat NEW_TARGET TARGET)
    if(CMAKE_VERSION VERSION_LESS "3.18")
        # Aliasing local targets is not supported on CMake < 3.18, so make it global.
        add_library(${NEW_TARGET} INTERFACE IMPORTED)
        set_target_properties(${NEW_TARGET} PROPERTIES INTERFACE_LINK_LIBRARIES "${TARGET}")
    else()
        add_library(${NEW_TARGET} ALIAS ${TARGET})
    endif()
endfunction()

# Make sure SDL3_net::SDL3_net always exists
if(NOT TARGET SDL3_net::SDL3_net)
    if(TARGET SDL3_net::SDL3_net-shared)
        _sdl_create_target_alias_compat(SDL3_net::SDL3_net SDL3_net::SDL3_net-shared)
    endif()
endif()

unset(_sdl3_net_arch_subdir)
unset(_sdl3_net_incdir)
unset(_sdl3_net_library)
unset(_sdl3_net_dll)

check_required_components(SDL3_net)
